# Copyright (C) 2018 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../gn/perfetto.gni")
import("../../gn/wasm.gni")

# This target is built twice: once with the default toolchain as a native C++
# library (for trace-based metrics / batch processing) and once as a WASM
# library (for the UI) by virtue of being depended on by the :wasm target below.
source_set("lib") {
  sources = [
    "sched.cc",
    "trace_processor.cc",
    "virtual_destructors.cc",
  ]
  if (is_wasm) {
    sources += [
      "emscripten_task_runner.cc",
      "emscripten_task_runner.h",
    ]
  }
  deps = [
    "../../gn:default_deps",
    "../../protos/perfetto/trace_processor:lite",
    "../../src/base",
  ]
}

wasm_lib("trace_processor") {
  sources = [
    "wasm_bridge.cc",
  ]

  # All the deps below are implicitly xxx($wasm_toolchain) because of the
  # outer "is_wasm" block.
  deps = [
    ":lib",
    "../../gn:default_deps",
    "../../protos/perfetto/trace_processor:lite",
  ]
}
