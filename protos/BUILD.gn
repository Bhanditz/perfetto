# Copyright (C) 2017 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../src/protozero/protozero_library.gni")
import("../gn/perfetto.gni")
import("../gn/proto_library.gni")

config("public_config") {
  include_dirs = [
    # This lets embedders include Perfetto protobufs with full paths
    # like "third_party/perfetto/protos/test_event.pbzero.h"
    "${root_gen_dir}/${perfetto_root_path}/protos_zero",
    "${root_gen_dir}/${perfetto_root_path}/protos_lite",

    # The below are needed due to generated protobuf headers including other
    # headers with a path relative to the perfetto root.
    # Unfortunately this means embedder code can include protos without
    # the "third_party/perfetto" prefix, and would be nice to somehow avoid.
    "${root_gen_dir}/${perfetto_root_path}/protos_zero/${perfetto_root_path}",
    "${root_gen_dir}/${perfetto_root_path}/protos_lite/${perfetto_root_path}",
  ]
}

proto_sources = [
  "test_event.proto",
  "trace_packet.proto",
  "trace.proto",
]

# Protozero generated stubs, for writers.
protozero_library("zero") {
  deps = [
    "ftrace:zero",
  ]
  sources = proto_sources
  proto_in_dir = perfetto_root_path
  proto_out_dir = "${perfetto_root_path}/protos_zero/${perfetto_root_path}"
  generator_plugin_options = "wrapper_namespace=pbzero"
}

# Protobuf Lite stubs, for readers.
proto_library("lite") {
  deps = [
    "ftrace:lite",
    "tracing_service:lite",
  ]
  sources = proto_sources
  proto_in_dir = perfetto_root_path
  proto_out_dir = "${perfetto_root_path}/protos_lite/${perfetto_root_path}"
}
